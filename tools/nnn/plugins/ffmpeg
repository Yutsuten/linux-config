#!/usr/bin/env fish
# --------------------------- #
# Process videos with ffmpeg. #
# --------------------------- #

source (status dirname)/.utils

nnn_get_selection FFMPEG process $argv[1] || return

for selection in $NNN_SELECTION
    echo >&2
    echo "[FFMPEG] Going to trim video: $selection" >&2
    echo >&2
    read --line --prompt-str '[FFMPEG] From > ' from || return
    read --line --prompt-str '[FFMPEG] Until> ' until || return
    echo >&2

    set options
    if test -n "$from"
        set --append options -ss $from
    end
    if test -n "$until"
        set --append options -to $until
    end
    if test -z "$options"
        echo Abort >&2
        return 1
    end

    echo '[FFMPEG] Processing...' >&2
    ffmpeg -loglevel warning $options -i $selection -codec copy -avoid_negative_ts make_zero -y /tmp/$selection || return
    touch --reference $selection /tmp/$selection

    echo "[FFMPEG] Before : $(du -h $selection | cut -f 1)" >&2
    echo "[FFMPEG] After  : $(du -h /tmp/$selection | cut -f 1)" >&2
    mpv --msg-level=all=warn /tmp/$selection

    echo >&2
    if gum confirm '[FFMPEG] Replace original?' --show-output >&2
        trash-put $selection
        mv /tmp/$selection $selection
    else
        rm --force -- /tmp/$selection
    end
end
