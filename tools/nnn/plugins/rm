#!/usr/bin/fish

# Get selected files
set target_files
if test -s ~/.config/nnn/.selection
    while read --null target
        set --append target_files $target
    end < ~/.config/nnn/.selection
else
    set target_files $nnn
end

# User interaction
echo
echo "Type YES to permanently REMOVE the following $(count $target_files) item(s):"
echo
set num 1
for target in $target_files
    echo "$num: $target"
    set num (math $num + 1)
end
echo
read --prompt-str 'Proceed? ' answer

# Remove execution
if test "$answer" = "YES"
    rm -rf -- $target_files

    # Clear selection
    printf '-' > $NNN_PIPE
end
