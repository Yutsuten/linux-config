#!/usr/bin/env fish
# ------------------------------------ #
# Encrypt and decrypt files using gpg. #
# ------------------------------------ #

source (status dirname)/.utils

nnn_get_selection GPG encrypt/decrypt $argv[1] || return

for selection in $NNN_SELECTION
    if test -f $selection
        switch $selection
            case '*.tar.zst.gpg' # Decrypt directory
                echo "[GPG] Decrypt and extract $selection"
                gpg --decrypt $selection | tar --zstd --extract || continue
            case '*.gpg' # Decrypt file
                set out_file (string replace --regex '\.gpg$' '' $selection)
                echo "[GPG] Decrypt $selection to $out_file"
                gpg --output $out_file --decrypt $selection || continue
                touch --reference $selection $out_file
            case '*' # Encrypt file
                echo "[GPG] Generating $selection.gpg"
                gpg --output $selection.gpg --encrypt --default-recipient-self $selection || continue
                touch --reference $selection $selection.gpg
        end
    else if test -d $selection # Encrypt directory
        echo "[GPG] Generating $selection.tar.zst.gpg"
        tar --zstd --create $selection | gpg --output $selection.tar.zst.gpg --encrypt --default-recipient-self || continue
    end
end
