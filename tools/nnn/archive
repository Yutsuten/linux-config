#!/usr/bin/env fish
set bold (tput bold)
set reset (tput sgr0)

# Get selected files
set selected_files
if test -s ~/.config/nnn/.selection
    while read --null selection
        set --append selected_files (string replace --regex "^$PWD/" '' $selection)
    end <~/.config/nnn/.selection
else
    set selected_files $nnn
end

echo
echo $bold"[ARCHIVE] Going to compress the following $(count $selected_files) item(s):"$reset
echo
for i in (seq (count $selected_files))
    echo "  $i: $selected_files[$i]"
end
echo

function get_choice
    echo 'Which format to compress to?' >&2
    echo >&2
    for i in (seq (count $choices))
        echo "  $i: $choices[$i]" >&2
    end
    echo >&2
    read --line --prompt-str 'Your choice: ' index_choice

    if string match --quiet --regex '^[0-9]+$' $index_choice && test $index_choice -ge 1 -a $index_choice -le (count $choices)
        echo $choices[$index_choice]
    end
end

if test (count $selected_files) -eq 1
    set selection $selected_files[1]

    if test -f $selection # Single file
        set choices .zst .gz .zip
        set extension (get_choice)
        switch $extension
            case .zst
                echo "Generating $selection.zst"
                zstd $selection
            case .gz
                echo "Generating $selection.gz"
                gzip --keep $selection
            case .zip
                echo "Generating $selection.zip"
                zip $selection.zip $selection
            case '*'
                echo Abort
                return 1
        end
    else if test -d $selection # Single directory
        set choices .tar.zst .tgz .tar .zip
        set extension (get_choice)
        switch $extension
            case .tar.zst
                echo "Generating $selection.tar.zst"
                tar --zstd --create --file $selection.tar.zst $selection
            case .tgz
                echo "Generating $selection.tgz"
                tar --gzip --create --file $selection.tgz $selection
            case .tar
                echo "Generating $selection.tar"
                tar --create --file $selection.tar $selection
            case .zip
                echo "Generating $selection.zip"
                zip -r $selection.zip $selection
            case '*'
                echo Abort
                return 1
        end
    end
else # Multiple files / directories
    set choices .tar.zst .tgz .tar .zip
    read --line --prompt-str 'Filename without extension: ' output
    if test -z $output
        echo Abort
        return 1
    end
    echo
    set extension (get_choice)
    switch $extension
        case .tar.zst
            echo "Generating $output.tar.zst"
            tar --zstd --create --file $output.tar.zst $selected_files
        case .tgz
            echo "Generating $output.tgz"
            tar --gzip --create --file $output.tgz $selected_files
        case .tar
            echo "Generating $output.tar"
            tar --create --file $output.tar $selected_files
        case .zip
            echo "Generating $output.zip"
            zip -r $output.zip $selected_files
        case '*'
            echo Abort
            return 1
    end
end
