#!/usr/bin/env fish
# --------------------------- #
# Process videos with ffmpeg. #
# --------------------------- #

source (status dirname)/.utils

nnn_get_selection FFMPEG process $argv[1] || return

for selection in $NNN_SELECTION
    echo
    echo "[FFMPEG] Going to trim video: $selection"
    echo
    read --line --prompt-str '[FFMPEG] From > ' from || return
    read --line --prompt-str '[FFMPEG] Until> ' until || return
    echo

    set options
    if test -n "$from"
        set --append options -ss $from
    end
    if test -n "$until"
        set --append options -to $until
    end
    if test -z "$options"
        echo Abort >&2
        return 1
    end

    echo '[FFMPEG] Processing...'
    ffmpeg -loglevel warning $options -i $selection -codec copy -avoid_negative_ts make_zero -y /tmp/$selection || return
    touch --reference $selection /tmp/$selection

    echo "[FFMPEG] Before : $(du -h $selection | cut -f 1)"
    echo "[FFMPEG] After  : $(du -h /tmp/$selection | cut -f 1)"
    mpv --msg-level=all=warn /tmp/$selection

    echo
    if gum confirm '[FFMPEG] Replace original?' --show-output
        trash-put $selection
        mv /tmp/$selection $selection
    else
        rm --force -- /tmp/$selection
    end
end
