#!/usr/bin/fish

# Get selected files
set selected_files
if test -s ~/.config/nnn/.selection
    while read --null selection
        set --append selected_files $selection
    end < ~/.config/nnn/.selection
else
    set selected_files $nnn
end

# User interaction
echo
echo "Type YES to permanently REMOVE the following $(count $selected_files) item(s):"
echo
set num 1
for target in $selected_files
    echo "$num: $target"
    set num (math $num + 1)
end
echo
read --prompt-str 'Proceed? ' answer

# Remove execution
if test "$answer" = "YES"
    rm -rf -- $selected_files

    # Clear selection
    printf '-' > $NNN_PIPE
end
