#!/usr/bin/fish

set bold (tput bold)
set reset (tput sgr0)

# Get selected files
set selected_files
if test -s ~/.config/nnn/.selection
    while read --null selection
        set --append selected_files (string replace --regex "^$PWD/" '' $selection)
    end <~/.config/nnn/.selection
else
    set selected_files $nnn
end

# User interaction
echo
echo $bold"[RM] Going to REMOVE the following $(count $selected_files) item(s):"$reset
echo
for i in (seq (count $selected_files))
    echo "  $i: $selected_files[$i]"
end
echo

# Remove execution
if gum confirm 'Are you sure?'
    rm --recursive --force -- $selected_files
    printf - >$NNN_PIPE # Clear selection
end
