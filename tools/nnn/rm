#!/usr/bin/fish

# Get selected files
set selected_files
if test -s ~/.config/nnn/.selection
    while read --null selection
        set --append selected_files $selection
    end < ~/.config/nnn/.selection
else
    set selected_files $nnn
end

# User interaction
echo
echo "Will REMOVE the following $(count $selected_files) item(s):"
echo
set num 1
for target in $selected_files
    echo "$num: $target"
    set num (math $num + 1)
end
echo
read --line --prompt-str 'Are you sure? [y/N] ' answer

# Remove execution
if string match --quiet --regex '^[Yy]$' $answer
    rm --recursive --force -- $selected_files

    # Clear selection
    printf '-' > $NNN_PIPE
end
