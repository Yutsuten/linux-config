#!/usr/bin/fish

set bold (tput bold)
set reset (tput sgr0)

# Get selected files
set selected_files
if test -s ~/.config/nnn/.selection
    while read --null selection
        set --append selected_files $selection
    end <~/.config/nnn/.selection
else
    set selected_files $nnn
end

echo
echo $bold"[GPG] Going to encrypt/decrypt the following $(count $selected_files) item(s):"$reset
echo
for i in (seq (count $selected_files))
    echo "  $i: $selected_files[$i]"
end
echo

# GPG execution
for selection in $selected_files
    if test -f $selection
        switch $selection
            case '*.tar.zst.gpg' # Decrypt directory
                echo "Decrypt and extract $selection"
                gpg --decrypt $selection | tar --zstd --extract || continue
            case '*.gpg' # Decrypt file
                set out_file (string replace --regex '\.gpg$' '' $selection)
                echo "Decrypt $selection to $out_file"
                gpg --output $out_file --decrypt $selection || continue
                touch --reference $selection $out_file
            case '*' # Encrypt file
                echo "Generating $selection.gpg"
                gpg --output $selection.gpg --encrypt --default-recipient-self $selection || continue
                touch --reference $selection $selection.gpg
        end
    else if test -d $selection # Encrypt directory
        echo "Generating $selection.tar.zst.gpg"
        tar --zstd --create $selection | gpg --output $selection.tar.zst.gpg --encrypt --default-recipient-self || continue
    end
end
