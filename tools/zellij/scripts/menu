#!/usr/bin/env fish
# --------------------------------------------------- #
# Zellij's main menu.                                 #
# Extra scripts can be added at ~/.local/share/zellij #
# --------------------------------------------------- #

zellij action rename-pane menu
set choices 'Open project' 'New project' 'Password Manager' 'File Browser' Fish 'Fish Private'
for filepath in ~/.local/share/zellij/*
    # Split on `-` AND capitalize
    set --erase choice
    for word in (string split - (path basename $filepath))
        set choice "$choice "(string sub --length 1 -- $word | string upper)(string sub --start 2 -- $word)
    end
    set --append choices (string trim $choice)
end

set selection (string join \n $choices | sort --version-sort | fzf) || return
switch $selection
    case 'Open project'
        source ~/.config/zellij/scripts/open-project
        return
    case 'New project'
        source ~/.config/zellij/scripts/new-project
        return
    case 'Password Manager'
        zellij action rename-tab Pass
        zellij action rename-pane fpass
        fpass --quiet
        return
    case 'File Browser'
        zellij action rename-tab Nnn
        zellij action rename-pane nnn
        cd ~ && nnn
        return
    case Fish
        zellij action rename-tab Fish
        zellij action rename-pane fish
        cd ~ && fish
        return
    case 'Fish Private'
        zellij action rename-tab Fish
        zellij action rename-pane 'fish --private'
        cd ~ && fish --private
        return
    case '*'
        set scriptname (string lower (string replace --all ' ' '-' $selection))
        if test -x ~/.local/share/zellij/$scriptname
            source ~/.local/share/zellij/$scriptname
            return
        end
end

echo 'Something went wrong...' >&2
read --prompt-str 'Press ENTER to continue...'
return 1
