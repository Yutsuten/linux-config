#!/usr/bin/env fish
# --------------------- #
# Create a new project. #
# --------------------- #

zellij action rename-pane new-project
set project_lang (gum choose --header 'New project language:' python rust) || return
while test -z "$project_name"
    set project_name (gum input --header 'Project name:') || return
end

switch $project_lang
    case python
        # Create project
        cd ~/Projects
        mkdir $project_name
        cd $project_name
        zellij-setup python
        set main src/main.py
    case rust
        # Create project
        cd ~/Projects
        cargo new $project_name
        cd $project_name
        zellij-setup rust
        set main src/main.rs
end

# TabName: Split on `-` AND capitalize
for word in (string split - $project_name)
    set tabname "$tabname"(string sub --length 1 -- $word | string upper)(string sub --start 2 -- $word)
end

zellij action rename-tab $tabname
zellij action rename-pane hx

exec hx $main
