#!/usr/bin/env fish
# --------------------- #
# Create a new project. #
# --------------------- #

zellij action rename-pane new-project

set AVAILABLE_LANGUAGES c cpp python rust
set project_lang (gum choose --header 'New project language:' $AVAILABLE_LANGUAGES) || return

while test -z "$PROJECT_NAME"
    set -gx PROJECT_NAME (gum input --header 'Project name:') || return
end

cd ~/Projects

switch $project_lang
    case c
        mkdir $PROJECT_NAME
        cd $PROJECT_NAME
        zellij-setup c
        set main src/main.c
    case cpp
        mkdir $PROJECT_NAME
        cd $PROJECT_NAME
        zellij-setup cpp
        set main src/main.cpp
    case python
        mkdir $PROJECT_NAME
        cd $PROJECT_NAME
        zellij-setup python
        source .venv/bin/activate.fish
        set main src/main.py
    case rust
        cargo new $PROJECT_NAME
        cd $PROJECT_NAME
        zellij-setup rust
        set main src/main.rs
end

# TabName: Split on `-` AND capitalize
for word in (string split - $PROJECT_NAME)
    set tabname "$tabname"(string sub --length 1 -- $word | string upper)(string sub --start 2 -- $word)
end

zellij action rename-tab $tabname
zellij action rename-pane hx

exec hx $main
