#!/usr/bin/env fish
# -------------------------------------------------- #
# Run scripts inside `.zellij` of current directory. #
# -------------------------------------------------- #

if not test -d .zellij
    echo '.zellij directory not found' >&2
    read --prompt-str "[Process exited 1]"
    return 1
end

set choices
for choice in .zellij/*
    if test -x $choice
        set --append choices (path basename $choice)
    end
end

set num_choices (count $choices)

if test $num_choices -eq 0
    echo 'There are no executable scripts in .zellij directory' >&2
    read --prompt-str "[Process exited 1]"
    return 1
else if test $num_choices -eq 1
    set selection $choices[1]
else
    set selection (string join \n $choices | sort | fzf --layout=reverse) || return
end

zellij action rename-tab "Exec $selection"
./.zellij/$selection
set result $status
echo
read --prompt-str "[Process exited $result]"
