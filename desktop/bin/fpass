#!/usr/bin/env fish
argparse h/help -- $argv
set exitcode $status

function help
    echo 'Usage: fpass [-h|--help]' >&2
    echo >&2
    echo '  Synopsis:' >&2
    echo '    Password manager using fuzzy finder' >&2
    echo >&2
    echo '  Options:' >&2
    echo '    -h, --help      Show list of command-line options' >&2
end

if test $exitcode -ne 0 || set --query --local _flag_help
    help
    return $exitcode
end

cd ~/.password-store

set pass_store_cache (mktemp)
set interrupt 0
set ignore_list

if test -s .gitignore
    while read --line line
        set line (string trim $line)
        if test -n "$line" && string match --quiet --invert '#*' "$line"
            set --append ignore_list $line
        end
    end <.gitignore
end

trap "rm $pass_store_cache" SIGHUP SIGINT SIGTERM

find . -type d -path '*/.*' -prune -or -type f -name '*.gpg' -printf '%P\n' | sort --version-sort | while read --line line
    set skip 0
    for ignore in $ignore_list
        if string match --quiet "$ignore*" $line
            set skip 1
            break
        end
    end
    if test $skip -eq 0
        path change-extension '' $line >>$pass_store_cache
    end
end

while test $interrupt -eq 0 && set selection (fzf --layout=reverse <$pass_store_cache)
    pass $selection
    pass --clip $selection
    read --line --prompt-str '' || set interrupt 1
end

rm $pass_store_cache
