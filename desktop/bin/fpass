#!/usr/bin/fish
argparse 'h/help' 's/show' -- $argv
set exitcode $status

function help
    echo 'Usage: fpass [-h|--help] [-s|--show]' >&2
    echo >&2
    echo '  Synopsis:' >&2
    echo '    Password manager using fuzzy finder' >&2
    echo >&2
    echo '  Options:' >&2
    echo '    -h, --help      Show list of command-line options' >&2
    echo '    -s, --show      Print the password to stdout' >&2
end

if test $exitcode -ne 0 || set --query --local _flag_help
    help
    return $exitcode
end

cd ~/.password-store

set pass_store_cache (mktemp)
set interrupt 0

trap "rm $pass_store_cache" SIGHUP SIGINT SIGTERM

find . -type d -path '*/.*' -prune -o -type f -name '*.gpg' -printf '%P\n' | while read --line line
    path change-extension '' $line >> $pass_store_cache
end

while test $interrupt -eq 0 && set selection (fzf < $pass_store_cache)
    if set --query _flag_show
        printf '%-30s %s\n' (pass $selection) $selection
    else
        pass --clip $selection
    end
    read --line --prompt-str '' || set interrupt 1
end

rm $pass_store_cache
