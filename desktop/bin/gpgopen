#!/usr/bin/fish
argparse --max-args 1 'h/help' -- $argv
set exitcode $status

function help
    echo 'Usage: gpgopen [-h|--help] FILE' >&2
    echo >&2
    echo '  Synopsis:' >&2
    echo '    Open gpg files using xdg-open.' >&2
    echo >&2
    echo '  Options:' >&2
    echo '    -h, --help      Show list of command-line options' >&2
    echo >&2
    echo '  Arguments:' >&2
    echo '    FILE: The gpg file to be opened' >&2
end

if test $exitcode -ne 0 -o (count $argv) -eq 0 || set --query --local _flag_help
    help
    return $exitcode
end

if not test -f $argv[1]
    echo "$argv[1] is not a file." >&2
    help
    return 1
end

if not string match --quiet --regex '\.gpg$' $argv[1]
    echo "$argv[1] should have .gpg extension." >&2
    return 1
end

set decrypted_file /tmp/(string replace --regex '\.gpg$' '' (path basename $argv[1]))

# Skip decrypt if opening twice
if not test -f $decrypted_file
    if not gpg --output $decrypted_file --decrypt $argv[1]
        echo "$argv[1] is not a gpg file" >&2
        return 1
    end
    touch --reference $argv[1] $decrypted_file
end

set before_shasum (sha512sum $decrypted_file)
xdg-open $decrypted_file

if test (sha512sum $decrypted_file) = $before_shasum
    echo "File was not edited." >&2
    # Workaround: PDFs opening in existing browser exit before being fully loaded
    if test (path extension $decrypted_file) = .pdf
        sleep 3s
    end
    rm --force -- $decrypted_file
    return 0
end

gpg --output - --encrypt --default-recipient-self $decrypted_file > $argv[1] && rm --force -- $decrypted_file
