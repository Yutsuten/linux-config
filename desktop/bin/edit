#!/usr/bin/env fish
argparse h/help -- $argv
set exitcode $status

function help
    echo 'Usage: edit [options] FILE' >&2
    echo >&2
    echo '  Synopsis:' >&2
    echo '    Edit text or gpg files using helix.' >&2
    echo >&2
    echo '  Options:' >&2
    echo '    -h, --help      Show list of command-line options' >&2
    echo >&2
    echo '  Arguments:' >&2
    echo '    FILE: The file to be edited' >&2
end

if test $exitcode -ne 0 -o (count $argv) -eq 0 || set --query --local _flag_help
    help
    return $exitcode
end

switch (path extension $argv[-1])
    case '.gpg'
        set decrypted_file (mktemp --tmpdir XXXXXXX_(path change-extension '' (path basename $argv[-1])))
        if not gpg --decrypt $argv[-1] >$decrypted_file
            echo "$argv[-1] is not a gpg file." >&2
            rm --force -- $decrypted_file
            return 1
        end

        touch --reference $argv[-1] $decrypted_file
        set before_shasum (sha512sum $decrypted_file)
        helix $argv[..-2] $decrypted_file

        if test (sha512sum $decrypted_file) = $before_shasum
            rm --force -- $decrypted_file
            return 0
        end
        gpg --output - --encrypt --default-recipient-self $decrypted_file >$argv[-1] && rm --force -- $decrypted_file
    case '*'
        helix $argv
end
